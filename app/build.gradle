apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.kanedias.vanilla.lyrics"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 14
        versionName "1.3.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('misc/signing.keystore')
            storePassword System.getenv('SIGNING_KEYSTORE_PASSWORD')
            keyAlias 'release-key'
            keyPassword System.getenv('RELEASE_KEY_PASSWORD')
        }
    }

    buildTypes {
        def geniusApiToken = file("misc/genius-api-token.txt").text

        debug {
            applicationIdSuffix = ".debug"
            buildConfigField "String", "GENIUS_API_TOKEN", "\"$geniusApiToken\""
        }

        release {
            buildConfigField "String", "GENIUS_API_TOKEN", "\"$geniusApiToken\""

            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }
}

play {
    serviceAccountCredentials = file("misc/android-publisher-account.json")
    track = 'production'
}

dependencies {
    implementation 'com.github.vanilla-music:vanilla-music-plugin-commons:1.0.3'
    implementation 'org.jsoup:jsoup:1.11.3'     // for parsing lyrics wiki pages
}
